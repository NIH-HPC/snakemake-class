# vim: set ft=python:

###
### Exercise 3 - incorporate the featureCounts chunk as a rule
###
import os.path
from glob import glob

samples = [os.path.basename(a).replace(".fastq.gz", "") for a in glob("00fastq/*.fastq.gz")]

rule all:
    input: expand("04count/{s}", s=samples)

rule clean:
    shell:
        """
        rm -rf 02aln 04count
        """

###
### FeatureCounts chunk - make this into a rule and incorporate it into the pipeline
###

rule count:    
    input: bam = "02aln/{sample}.bam",
           annot = "00ref/R64-1-1.genes.gtf"
    output: "04count/{sample}"
    threads: 2
    resources: mem_mb = 4096
    singularity:
        "library://wresch/classes/rnaseq:0.8"
    shell:
        """
        featureCounts -a {input.annot} -o {output} \
                -T {threads} --minOverlap 10 {input.bam}
        """

rule hisat2:
    input: fq = "00fastq/{sample}.fastq.gz",
           idx = "00ref/hisat_index/R64-1-1"
    output: bam = "02aln/{sample}.bam",
            bai = "02aln/{sample}.bam.bai"
    threads: 4
    resources: mem_mb = 6144
    params: hisat = "-k 4"
    singularity:
        "library://wresch/classes/rnaseq:0.8"
    shell:
        """
        hisat2 {params.hisat} -x {input.idx} -U {input.fq} --threads {threads} \
          | samtools sort -T tmp/{wildcards.sample} -O BAM \
          > {output.bam}
        samtools index {output.bam}
        """
 
